Rem ***** Included Source File *****

type player_type
   x
   y
   alive
endtype

function player_init()
   set sprite 2,0,1
   player.x = screen width()/2 - 28/2
   player.y = screen height() - 21*2
   player.alive = 1
endfunction

function player_input()
   if spacekey()=1 and spacekey_down=0
       spacekey_down = 1
    else
       if spacekey_down=1 and player.alive = 1
          play sound 1
          laser_spawn()
          spacekey_down = -1
       endif
    endif
    if spacekey()=0
       spacekey_down = 0
    endif
    
    if joystick fire a()=1 and fire_a_down=0 
       fire_a_down = 1
    else
       if fire_a_down=1 and player.alive = 1
          play sound 1
          laser_spawn()
          fire_a_down = -1
       endif
    endif
    if joystick fire a()=0
       fire_a_down = 0
    endif

    if leftkey()=1 or joystick x()<=-300 
       player.x = player.x - 10
    endif

    if rightkey()=1 or joystick x()>=300
       player.x = player.x + 10
    endif
    player.x = clampn(player.x, 0, screen width() - sprite width (2))
endfunction

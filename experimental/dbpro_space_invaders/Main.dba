rem Project: Space Invaders
rem Created: Thursday, December 23, 2021
rem Created by Memorix101
rem Written using version 12.02.16 - Dark Basic Pro Binary 120216
rem Open Source release https:remgithub.com/TheGameCreators/Dark-Basic-Pro/releases/tag/12.02.16

rem ***** Main Source File *****

rem #include "./Utils.dba"
rem #include "./Player.dba"
rem #include "./Enemy.dba"
rem #include "./Laser.dba"
rem #include "./Explosion.dba"

sync on : sync rate 60 : backdrop off
set display mode 640,480,32
set window on
disable escapekey

rem setup global game variables
score = 0
gameover = 0
quit = 0
global spacekey_down = 0
global fire_a_down = 0

rem setup gloabl game assets
LoadFont("rd/vermin_vibes_1989.ttf") : rem installing font temporary into system
set text font "Vermin Vibes 1989 Regular" : rem does not work but should? https://forum.thegamecreators.com/thread/63687
set text size 20

load image "rd/space3.png",1
load image "rd/player.png",2
load image "rd/bullet.png", 4
load image "rd/enemy-bullet.png",6
load image "rd/gameover_ui.png",7
load image "rd/fmg_splash.png",8
load image "rd/win_ui.png",9

sprite 7000, 0, 0, 7
hide sprite 7000
sprite 7001, 0, 0, 9
hide sprite 7001

rem create player
dim laser(0) as laser_type
dim enemy_laser(0) as enemy_laser_type
player as player_type
player_init()

rem create enemy - sprite id 3 to 42
dim enemy(0) as enemy_type
enemy_init()
dim explo(0) as explo_type

rem load audio
load music "rd/bodenstaendig.mp3",1
loop music 1

load sound "rd/blaster.wav",1
load sound "rd/explode1.wav",2
load sound "rd/pusher.wav",3

wait_ = 0
do
	sprite 43,0,0,8
	inc wait_

		if wait_ > 150
			delete sprite 43
			exit
		endif
	sync
loop 

rem main loop
repeat
   rem player input
   player_input()
   enemy_update()
   laser_update()

   rem enemy is sprite id 3 to 42
   for e=0 to array count(enemy())-1
      for l=0 to array count(laser())-1
         if sprite collision (enemy(e).sprite_id, laser(l).sprite_id) and enemy(e).alive = 1 and laser(l).kill_me = 0
            laser(l).kill_me = 1
            enemy(e).alive = 0
            laser(l).x = -10 : rem we hide the sprites off screen until they get reused
            hide sprite enemy(e).sprite_id
            score = score + 100
            play sound 2
            explo_spawn(enemy(e).x - 64+16, enemy(e).y - 64+16)
         endif
      next l
   next e

   for t=0 to array count(enemy_laser())-1
         if sprite collision (enemy_laser(t).sprite_id, 2) and player.alive = 1
            player.alive = 0
            explo_spawn(player.x - 64+16, player.y - 64+16)
            hide sprite 2
            play sound 2
            gameover = 1
            show sprite 7000
         endif
   next t

   if score >= 4000
      gameover = 1
      show sprite 7001
   endif

   rem draw stuff here
   paste image 1,0,0 : rem drawing background as image because sprites are drawn over text
   sprite 2, player.x, player.y, 2 : rem draw and set position of player

   rem enemy draw stuff
   for t=0 to array count(enemy())-1 rem enemy is sprite id 3 to 42
      play sprite enemy(t).sprite_id,1,4,60
      sprite enemy(t).sprite_id, enemy(t).x, enemy(t).y, 3
   next t

   rem explo draw stuff
   for t=0 to array count(explo())-1 : rem explo is sprite id 70 to something
      play sprite explo(t).sprite_id,1,16,60
      if sprite frame(explo(t).sprite_id) = 16
         hide sprite explo(t).sprite_id
      endif
   next t

   rem score text formatting
   score_numbers$ = "";
   pad = len(str$(score))
   for t=1 to 4-pad
      score_numbers$ = score_numbers$ + "0"
   next t
   score_numbers$ = score_numbers$ + str$(score)
   rem score text
   text screen width()-text width("SCORE: 0000")-20, 20, "SCORE: "+score_numbers$

   rem show framerate
   rem text 20,screen height()-40,desc$
   rem fps$="FPS: "+str$(screen fps())
   rem text screen width()-5-text width(fps$),screen height()-20,fps$

   rem debug text stuff
   set cursor 0,0
   rem print "Player_X: "+str$(player.x)
   rem print "Enemy Pos: "+str$(enemy(0).x)
   rem print "Laser Array: "+str$(array count(laser()))
   rem print "Explo Array: "+str$(array count(explo()))
   rem print "cur_explo: "+str$(cur_explo)
   rem print "timer: "+str$(shoot_counter)
   remstart print "joystick left "+str$(joystick left())
	print "joystick right "+str$(joystick right())
 	print "joystick x "+str$(joystick x())
	print "joystick y "+str$(joystick y())
	print "joystick z "+str$(joystick z())
	print "joystick fire a "+str$(joystick fire a())
	print "joystick fire b "+str$(joystick fire b())
	print "joystick fire c "+str$(joystick fire c())
	print "joystick fire d "+str$(joystick fire d())
	print "joystick fire x "+str$(joystick fire x(0)) remend
	remstart print "joystick hat angle(0)"+str$(joystick hat angle(0))
	print "joystick hat angle(1)"+str$(joystick hat angle(1))
	print "joystick hat angle(2)"+str$(joystick hat angle(2))
	print "joystick hat angle(3)"+str$(joystick hat angle(3)) remend

   if returnkey()=1 and gameover=1 or joystick fire b()=1 and gameover=1
      gameover = 0
      score = 0
      shoot_counter = 0
      player_init()
      show sprite 2
      hide sprite 7000 : rem hide gameover ui
      hide sprite 7001 : rem hide win ui
      for t=0 to 39 rem enemy is sprite id 3 to 42
         show sprite enemy(t).sprite_id
         enemy(t).alive = 1
      next t
   endif

   if escapekey()=1
      UnloadFont("rd/vermin_vibes_1989.ttf")
      wait 1000
         quit = 1
   endif
   sync : rem update screen
until quit = 1: rem end of loop
end
